## Grammar by James Clark.  Funky Perl by yours truely.

%{
    use Carp;
    use UNIVERSAL;
    use XML::Filter::Dispatcher::Ops;

    sub _no {
        my $p = shift;
#        push @{$p->{USER}->{NONONO}}, join(
die join(
            "",
            "XPath construct not supported: ",
            join( " ", map
                defined $_
                    ? ref $_
                        ? do {
                            my $f = ref $_;
                            $f =~ s/^XFD:://;
                            $f;
                        }
                        : $_
                    : "<undef>" ,
                @_
            ),
            " (grammar rule at ",
            (caller)[1],
            ", line ",
            (caller)[2],
            ")"
        );

        return ();
    }

    sub _step {
        my ( $p, $axis, $node_test, @predicates ) = @_;

        shift @predicates if @predicates && ! defined $predicates[0];

        warn "axis not an op: '$axis'" if defined $axis && !ref $axis;
        warn "node test not an op: '$node_test'" if defined $node_test && !ref $node_test;

        my @ops;
        push @ops, $axis if defined $axis && ref $axis;
        push @ops, $node_test if $node_test && ref $node_test;
        push @ops, @predicates;

        for ( 0..$#ops-1 ) {
            $ops[$_]->set_next( $ops[$_+1] );
        }
            
        return $ops[0];
    }
%}

%token QNAME
%token NAME_COLON_STAR
%token DOT
%token DOT_DOT
%token AT
%token AXIS_NAME
%token FUNCTION_NAME
%token COMMENT
%token PI
%token TEXT
%token NODE
%token STAR
%token LPAR
%token RPAR
%token LSQB
%token RSQB
%token LITERAL
%token NUMBER
%token COLON_COLON
%token DOLLAR_QNAME
%token SLASH
%token SLASH_SLASH
%token VBAR
%token COMMA
%token PLUS
%token MINUS
%token EQUALS
%token GT
%token LT
%token GTE
%token LTE
%token MULTIPLY
%token AND
%token OR
%token MOD
%token DIV
# %token QUO

## We also catch some Perl tokens so we can give useful advice
%token EQUALS_EQUALS
%token VBAR_VBAR
%token AMP_AMP

%%

## NOTE: I use the paren-less format for Perl subcalls here so that
## perl will warn me if I don't have one defined.

expr :
    or_expr
    ;

or_expr :
    and_expr
    | or_expr OR and_expr                  { XFD::or [ @_[1,3] ] }
    | or_expr VBAR_VBAR and_expr           {
        die "XPath uses 'or' instead of Perl's '||'\n";
    }
    ;

and_expr :
    equality_expr
    | and_expr AND equality_expr           { XFD::and [ @_[1,3] ] }
    | and_expr AMP_AMP equality_expr       {
        die "XPath uses 'and' instead of Perl's '&&'\n";
    }
    | and_expr AMP equality_expr       {
        die "XPath uses 'and' instead of Perl's '&'\n";
    }
    ;

equality_expr :
    relational_expr
    | equality_expr EQUALS relational_expr      { XFD::equals [ @_[1,3] ] }
    | equality_expr BANG_EQUALS relational_expr { XFD::not_equals [ @_[1,3] ] }
    | equality_expr EQUALS_EQUALS relational_expr { 
        die "XPath uses '=' instead of Perl's '=='\n";
    }
    ;

relational_expr :
    additive_expr
    | relational_expr LT additive_expr    { XFD::lt  [ @_[1,3] ] }
    | relational_expr GT additive_expr    { XFD::gt  [ @_[1,3] ] }
    | relational_expr LTE additive_expr   { XFD::lte [ @_[1,3] ] }
    | relational_expr GTE additive_expr   { XFD::gte [ @_[1,3] ] }
    ;

additive_expr :
    multiplicative_expr
    | additive_expr PLUS multiplicative_expr  { XFD::addition    [ @_[1,3] ] }
    | additive_expr MINUS multiplicative_expr { XFD::subtraction [ @_[1,3] ] }
    ;

multiplicative_expr :
    unary_expr
    | multiplicative_expr MULTIPLY unary_expr { XFD::multiplication[ @_[1,3] ] }
    | multiplicative_expr DIV unary_expr      { XFD::division      [ @_[1,3] ] }
    | multiplicative_expr MOD unary_expr      { XFD::modulus       [ @_[1,3] ] }
    ;

unary_expr :
    union_expr
    | MINUS unary_expr                        { XFD::negation $_[2] }
    ;

union_expr :
    path_expr
    | union_expr VBAR path_expr          {
        my $union;
        if ( $_[1]->isa( "XFD::union" ) ) {
            $_[1]->add( $_[3] );
            $union = $_[1];
        }
        else {
            $union = XFD::union->new( @_[1,3] )
        }
        $union;
    }
    ;

path_expr :
    location_path
    | primary_expr predicates segment    {
        if ( defined $_[2] ) {
            die "This XPath implementation has no node sets, (expression)[predicate] is not supported\n";
        }
        if ( defined $_[3] ) {
            die "This XPath implementation has no node sets, (expression)/path is not supported\n";
        }
        $_[1];
    }
    ;

segment :
    /* empty */
    | SLASH relative_location_path       
    | SLASH_SLASH relative_location_path
    ;

location_path :
    relative_location_path
    | absolute_location_path
    ;

absolute_location_path :
    SLASH                                { XFD::doc_node->new }
    | SLASH relative_location_path       {
        my $op = XFD::doc_node->new( @_[0..1] );
        $op->set_next( $_[2] );
        $op;
    }
    | SLASH_SLASH relative_location_path { 
        ## /descendant-or-self::node()/relative_location_path
        my $op = XFD::doc_node->new( @_[0..1] );
        my $step = _step(
            $_[0],
            XFD::Axis::descendant_or_self->new,
            XFD::NodeType::node->new,
        );
        $op->set_next( $step );
        $step->set_next( $_[2] );
        $op;
    };

relative_location_path :
    step
    | relative_location_path SLASH step    { $_[1]->set_next( $_[3] ) ; $_[1] }
    ## This next rule means that the grammar does not like "////" :(.
    ## TODO: add a rule for successive "//" paths
    | relative_location_path SLASH_SLASH step {
        my $step = _step(
            $_[0],
            XFD::Axis::descendant_or_self->new,
            XFD::NodeType::node->new,
        );
        $_[1]->set_next( $step );
        $step->set_next( $_[3] );
        $_[1];
    }
    ;

step :
    axis node_test predicates       { _step( @_ ) }
    | DOT                           { XFD::self_node->new }
    | DOT_DOT { _no @_; }
    ;

axis:
    /* empty */                 { XFD::Axis::child->new     }
    | AXIS_NAME COLON_COLON     { XFD::axis( $_[1] )        }
    | AT                        { XFD::Axis::attribute->new }
    ;

predicates :
    /* empty */
    | predicates LSQB expr RSQB {
        my $p = XFD::predicate->new( $_[3] );
        if ( defined $_[1] ) {
            $_[1]->set_next( $p );
            return $_[1];
        }
        return $p;
    }
    ;

primary_expr :
    DOLLAR_QNAME     { XFD::get_var $_[1] }
    | LPAR expr RPAR { XFD::parens $_[2] }
    | LITERAL
    | NUMBER
    | function_call ## provisional
    ;

function_call :
    FUNCTION_NAME LPAR opt_args RPAR { XFD::function( @_[1,3] ) }
    ;

opt_args :
    /* empty */             { [] }
    | args                  ## pass thru
    ;

args :
    expr              {
        [ $_[1] ];
    }
    | args COMMA expr {
        push @{$_[1]}, $_[3];
        $_[1];
    }
    ;
  
node_test :
    QNAME                           { XFD::node_name->new( $_[1] ) }
    | STAR                          { XFD::any_node_name->new; }
    | NAME_COLON_STAR { _no @_; }
    | PI LPAR opt_literal RPAR      { XFD::NodeType::processing_instruction
                                                           ->new( $_[0] ) }
    | COMMENT LPAR RPAR             { XFD::NodeType::comment->new( $_[0] ) }
    | TEXT LPAR RPAR                { XFD::NodeType::text   ->new( $_[0] ) }
    | NODE LPAR RPAR                { XFD::NodeType::node   ->new( $_[0] ) }
    ;

opt_literal :
    /* empty */
    | LITERAL { _no @_; }
    ;

%%

=head1

XML::Filter::Dispatcher::Parser - Parses the XPath subset used by ...::Dispatcher

=head1 SYNOPSIS

   use XML::Filter::Dispatcher::Parser;

   my $result = XML::Filter::Dispatcher::Parser->parse( $xpath );

=head1 DESCRIPTION

Some notes on the parsing and evaluation:

=over

=item *

Result Objects

The result expressions alway return true or false.  For XPath
expressions that would normally return a node-set, the result is true if
the current SAX event would build a node that would be in the node set.
No floating point or string return objects are supported (this may
change).

=item *

Context

The XPath context node is the document root (theoretically; in reality
there is none).  The variables are the Dispatcher's data members, and
the function library is XXX.

Not sure what to do about the context position, but the context size is
of necessity undefined.

The namespace mapping will be added in when I grok the NamespaceHelper.

=back

=cut

use Carp;

my %tokens = (qw(
    .           DOT
    ..          DOT_DOT
    @           AT
    *           STAR
    (           LPAR
    )           RPAR
    [           LSQB
    ]           RSQB
    ::          COLON_COLON
    /           SLASH
    //          SLASH_SLASH
    |           VBAR
    +           PLUS
    -           MINUS
    =           EQUALS
    !=          BANG_EQUALS
    >           GT
    <           LT
    >=          GTE
    <=          LTE

    ==          EQUALS_EQUALS
    ||          VBAR_VBAR
    &&          AMP_AMP
    &           AMP
),
    "," =>      "COMMA"
);

my $simple_tokens =
    join "|",
        map
            quotemeta,
            reverse
                sort {
                    length $a <=> length $b
                } keys %tokens;

my $NCName = "(?:[a-zA-Z_][a-zA-Z0-9_.-]*)"; ## TODO: comb. chars & Extenders

my %NodeType = qw(
    node                   NODE
    text                   TEXT
    comment                COMMENT
    processing-instruction PI
);

my $NodeType = "(?:" .
    join( "|", map quotemeta, sort {length $a <=> length $b} keys %NodeType ) .
    ")";

my $AxisName = "(?:" .  join( "|", split /\n/, <<AXIS_LIST_END ) . ")" ;
ancestor
ancestor-or-self
attribute
child
descendant
descendant-or-self
following
following-sibling
namespace
parent
preceding
preceding-sibling
self
AXIS_LIST_END

my %preceding_tokens = map { ( $_ => undef ) } ( qw(
    @ :: [
    and or mod div
    *
    / // | + - = != < <= > >=

    == & && ||
    ),
    "(", ","
) ;

sub debugging () { 0 }

sub lex {
    my ( $p ) = @_;

    ## Optimization notes: we aren't parsing War and Peace here, so
    ## readability over performance.

    my $d = $p->{USER};
    my $input = \$d->{Input};

    ## This needs to be more contextual, only recognizing axis/function-name
    if ( ( pos( $$input ) || 0 ) == length $$input ) {
        $d->{LastToken} = undef;
        return ( '', undef );
    }

    my ( $token, $val ) ;
    ## First do the disambiguation rules:

    ## If there is a preceding token and the preceding token is not
    ## one of "@", "::", "(", "[", "," or an Operator,
    if ( defined $d->{LastToken}
        && ! exists $preceding_tokens{$d->{LastToken}}
    ) {
        ## a * must be recognized as a MultiplyOperator
        if ( $$input =~ /\G\s*\*/gc ) {
            ( $token, $val ) = ( MULTIPLY => "*" );
        }
        ## an NCName must be recognized as an OperatorName.
        elsif ( $$input =~ /\G\s*($NCName)/gc ) {
            die "Expected and, or, mod or div, got '$1'"
                unless 0 <= index "and|or|mod|div", $1;
            ( $token, $val ) = ( uc $1, $1 );
        }
    }

    ## NOTE: \s is only an approximation for ExprWhitespace
    unless ( defined $token ) {
        $$input =~ m{\G\s*(?:
            ## If the character following an NCName (possibly after
            ## intervening ExprWhitespace) is (, then the token must be
            ## recognized as a NodeType or a FunctionName.

            ($NCName)\s*(?=\()

            ## If the two characters following an NCName (possibly after
            ## intervening ExprWhitespace) are ::, then the token must be
            ## recognized as an AxisName

            |($NCName)\s*(?=::)

            |($NCName:\*)                           #NAME_COLON_STAR
            |((?:$NCName:)?$NCName)                 #QNAME
            |('[^']*'|"[^"]*")                      #LITERAL
            |(-?\d+(?:\.\d+)?|\.\d+)                #NUMBER
            |\$((?:$NCName:)?$NCName)               #DOLLAR_QNAME
            |($simple_tokens)
        )\s*}gcx;

        ( $token, $val ) =
            defined $1  ? (
                exists $NodeType{$1}
                    ? ( $NodeType{$1}, $1 )
                    : ( FUNCTION_NAME => $1 )
            ) :
        
            defined $2  ? (
                0 <= index( $AxisName, $2 )
                    ? ( AXIS_NAME => $2 )
                    : die "Expected an Axis Name, got '$2' at ",
                        pos $p->{USER}->{Input},
                        "\n"
            ) :
            defined  $3 ? ( NAME_COLON_STAR  =>  $3 ) :
            defined  $4 ? ( QNAME            =>  $4 ) :
            defined  $5 ? ( LITERAL          =>  do {
                    my $s = substr( $5, 1, -1 );
                    $s =~ s/([\\'])/\\$1/g;
                    bless \"'$s'", "string constant";
                }
            ) :
            defined  $6 ? ( NUMBER           =>  bless \"$6", "number constant" ) :
            defined  $7 ? ( DOLLAR_QNAME     =>  $7 ) :
            defined  $8 ? ( $tokens{$8}      =>  $8 ) :
            die "Failed to parse '$$input' at ",
                pos $$input,
                "\n";

        ## the parser needs to know whether an path expression is being
        ## parsed in a predicate or not so it can deal with paths
        ## using immediate code in predicates instead of converting them
        ## to incremental code run as precursors.
        if ( $p->{USER}->{ExitedPredicate} ) {
            --$XFD::predicate_depth;
        }
        $p->{USER}->{ExitedPredicate} = $token eq "RSQB";

        if ( $token eq "LSQB" ) {
            ++$XFD::predicate_depth;
        }
    }

    $d->{LastToken} = $val;

    if ( debugging ) {
        warn
            "'",
            $$input,
            "' (",
            pos $$input,
            "):",
            join( " => ", map defined $_ ? $_ : "<undef>", $token, $val ),
            "\n";
    }

    return ( $token, $val );
}


sub error {
    my ( $p ) = @_;
    print "Couldn't parse '$p->{USER}->{Input}' at position ", pos $p->{USER}->{Input}, "\n";
}

sub parse {
    my $self = shift;
    my ( $xpath, $action_code, $options ) = @_;

    $XFD::has_start_or_end = 0;

    my $p = XML::Filter::Dispatcher::Parser->new(
        yylex   => \&lex,
        yyerror => \&error,
        ( $options->{Debug} || 0 ) > 5
            ? ( yydebug => 0x1D )
            : (),
    );

    local $XFD::fold_constants =
        defined $options->{FoldConstants}
            ? $options->{FoldConstants}
            : 1;

    local $XFD::self_curriers = 0;
    local $XFD::rule_number   = $options->{RuleNumber};
    local $XFD::predicate_depth = 0;

    %{$p->{USER}} = %$options if $options;
    $p->{USER}->{ExitedPredicate} = 0;
    $p->{USER}->{Input} = $xpath;
    my $r = $p->YYParse;

    if ( $p->{USER}->{ExitedPredicate} ) {
        --$XFD::predicate_depth;
        $p->{USER}->{ExitedPredicate} = 0;
    }

    die map "$_\n", @{$p->{USER}->{NONONO}}
        if $p->{USER}->{NONONO} ;

    return undef unless defined $r;

    die "grammar returned '$r', needed a ref\n"
        unless ref $r;

    die "predicate_depth not zero: $XFD::predicate_depth"
        if $XFD::predicate_depth;

    package XFD;
    if ( $r->isa( "XFD::PathTest" ) ) {
        ## It's a relative location path, so 
        ## convert it to code as-is.
        $r = _rel2abs( $r )->as_incr_code( action \$action_code );
    }
    else {
        ## It's not a location path opcode, it's an expression,
        ## so make it get evaluated for every node 
        $r = ${ expr_eval $r, action \$action_code };
        $r = ${ all_nodes_or_self \$r }
            unless @XFD::precursors;
    }

    return () unless defined $r && length $r;

    ## Ok, looks like we have an expression that might be true sometimes,
    ## assemble it and any precursors in to a list of test subs.
    return XFD::expression_as_incr_code $r;

}

1 ;
